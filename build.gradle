import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    id "org.jetbrains.intellij" version "0.4.11"
    id 'com.palantir.git-version' version "0.11.0"
}

def htmlFixer = { htmlFile -> file(htmlFile).text.replace('<html>', '').replace('</html>', '') }

apply plugin: 'idea'

apply plugin: 'java'
sourceCompatibility = 1.8
targetCompatibility = 1.8

apply plugin: 'org.jetbrains.intellij'

intellij {
    localPath '/Applications/PhpStorm 2020.2 EAP.app'
    type 'PS'
    updateSinceUntilBuild false
    plugins = [
            "php",
            "twig",
            "dql",
            "de.espend.idea.php.annotation:${annotationPluginVersion}",
            "de.espend.idea.php.toolbox:${toolboxPluginVersion}",
            'webDeployment',
            'yaml',
            'CSS',
            'xpath'
    ]
    pluginName 'Symfony Support'

    // Can't instantiate configurable for PHP Toolbox
    // at de.espend.idea.php.toolbox.ui.application.ToolboxApplicationForm.<init>(ToolboxApplicationForm.java:26)
    tasks {
        "buildSearchableOptions" {
            enabled = false
        }
    }
}

patchPluginXml {
    sinceBuild '192'
    changeNotes = htmlFixer('src/main/resources/META-INF/change-notes.html')
}

publishPlugin {
    token System.getenv('IJ_TOKEN')
}

group 'fr.adrienbrault.idea.symfony2plugin'

def details = versionDetails()
if (details.isCleanTag) {
    version = "${details.lastTag}"
} else {
    version = "${details.lastTag}.${details.gitHash}-SNAPSHOT"
}

wrapper {
    gradleVersion '5.5.1'
}

test.testLogging.exceptionFormat = TestExceptionFormat.FULL
